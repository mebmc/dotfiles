# ZSH User

# Aliases and functions

alias ca="c $HOME/.config/nvim"
alias cv="c $HOME/.config/astronvim"
alias colors="~/.scripts/colors.sh"

## Editor

export NVIM_APPNAME="nvim"

alias e='nvim'
alias et='nvim +":Neotree"'
alias es='nvim +":Neotree show" +":SessionManager load_current_dir_session"'
alias ve='neovide'
alias hex="hexyl"

alias astro="NVIM_APPNAME=nvim-astro nvim"
alias chad="NVIM_APPNAME=nvim-chad nvim"
alias lazy="NVIM_APPNAME=nvim-lazy nvim"
alias ks="NVIM_APPNAME=nvim nvim"

function ea () {
  ( cd ~/.config/nvim && fd . | fzf --query="$*" --prompt='  Astronvim Config  ' --height=~50% --layout=reverse --border --exit-0 ) | xargs -I {} nvim $HOME/.config/nvim/{}
}

function ev () {
  ( cd ~/.config/astronvim && fd . | fzf --query="$*" --prompt='  Neovim Config  ' --height=~50% --layout=reverse --border --exit-0 ) | xargs -I {} nvim $HOME/.config/astronvim/{}
}

function nvims() {
  items=("default" "nvim-lazy" "nvim-astro" "nvim-chad" "nvim-test")
  config=$(printf "%s\n" "${items[@]}" | fzf --prompt=" Neovim Config  " --height=~50% --layout=reverse --border --exit-0)
  if [[ -z $config ]]; then
    echo "Nothing selected"
    return 0
  elif [[ $config == "default" ]]; then
    config=""
  elif [[ $config == "Test" ]]; then
    config="VimTest"
  fi
  NVIM_APPNAME=$config nvim $@
}

function eg() {
  git ls-files -cm | fzf --query="$*" --prompt=' 󰊢 Edit Git File  ' --height=50 --layout=reverse --border --exit-0 --preview="head -n100 {}" | xargs nvim
}

function ef() {
  fd -H --exclude=".git/" | fzf --query="$*" --prompt='   Edit File  ' --height=50 --layout=reverse --border --exit-0 --preview="head -n100 {}" | xargs nvim
}

## Lights

function scene() {
  openhue get scene -r $HUE_SCENE --json | jq '.[] | .Name' -r | sort | fzf --height=20% --layout=reverse --query="$1" --border --exit-0 | xargs -I {} openhue set scene -r $HUE_SCENE "{}"
}

function light() {
  LIGHT_LEVEL=$(seq 100 -5 0 | fzf --height=20% --layout=reverse --query="$1" --border --exit-0)
  if [[ "$LIGHT_LEVEL" -gt 0 ]]; then
    openhue set light -r $HUE_SCENE fb4f24a2-6530-467d-8c19-f31e6ad2183a --on --brightness "$LIGHT_LEVEL"
  else
    openhue set light -r $HUE_SCENE fb4f24a2-6530-467d-8c19-f31e6ad2183a --off
  fi
}

function light-off() {
  openhue set light -r $HUE_SCENE fb4f24a2-6530-467d-8c19-f31e6ad2183a --off
}

## Yadm

alias y="yadm"
alias ya="yadm add"
alias yc="yadm status -s"
alias yu="yadm status -s -u"

function ye () {
  yadm list -a | fzf --query="$*" --prompt='  Dotfile Config  ' --height=~50% --layout=reverse --border --exit-0 | xargs -I {} nvim $HOME/{}
}


# Tmux

alias attach="tmux new -A -s gen"

## Zellij

alias z="zellij"
alias za="zellij attach -c"
alias zp="zellij -l _personal -s personal 2>/dev/null || zellij attach personal"
alias zw="zellij -l _work -s work 2>/dev/null || zellij attach work"
alias zc="zellij -l _config -s config 2>/dev/null || zellij attach config"
alias zs="zellij list-sessions"
alias zt="zellij action new-tab"

## DevOps

alias a="ansible"
alias ap="ansible-playbook"
alias ansible-vault="ANSIBLE_VERBOSITY=0 ansible-vault"
alias aw="aws"
alias j="just"
alias tf="terraform"
alias wtf="wrapped-terraform"
alias tg="terragrunt"
alias gl="glab"

# alias ssh="ssh"
alias ssm="aws ssm start-session --target"
alias sso="AWS_PROFILE=work-live aws sso login"
alias eks="aws eks update-kubeconfig --name"

function worktree () {
  SELECTED_TREE=$( ( git worktree list | grep -v "\.bare" ) | fzf --prompt="Select Git worktree: " --height="40%" --layout=reverse --query="$1" --select-1 | awk '{print $1}' )
  cd $SELECTED_TREE
}
alias wt="worktree"

alias clear-profile="unset AWS_PROFILE"
function profile () {
  SELECTED_PROFILE=$( ( aws configure list-profiles && echo "[clear]" ) | fzf --prompt="Select AWS Profile: " --height="40%" --query="$1" --select-1 )
  if [[ "$SELECTED_PROFILE" != "" ]]; then
    if [[ "$SELECTED_PROFILE" != '[clear]' ]]; then
      export AWS_PROFILE="$SELECTED_PROFILE"
    else
      unset AWS_PROFILE
    fi
  fi
}

function cluster () {
  SELECTED_CLUSTER=$( kubectl config get-contexts --no-headers -o name | fzf --prompt="Select Kubernetes Cluster: " --height="40%" --query="$1" --select-1 )
  if [[ "$SELECTED_CLUSTER" != "" ]]; then
    kubectl config use-context "$SELECTED_CLUSTER"
  fi
}

function ports () {
  echo $1
  if [[ "$1" == "" ]]; then
    lsof -i -nP | grep LISTEN
  else
    lsof -i -nP | grep LISTEN | grep "$1"
  fi
}

## System

alias u="unistry"
alias de="direnv"
alias vg="lazygit"

## SSH


function ssp() {
  HOST=$(rg --no-heading -IN "^Host" $HOME/.ssh/config.d/hosts_personal* \
    | sd ' ' '\n' | grep -vE "(^Host|\*)" \
    | fzf --query="$*" --prompt=' 󱘖 SSH Personal  ' --height=~50% --layout=reverse --border --exit-0)
  ssh $HOST
}

function ssw() {
  HOST=$(rg --no-heading -IN "^Host" $HOME/.ssh/config.d/hosts_work* \
    | sd ' ' '\n' | grep -vE "(^Host|\*)" \
    | fzf --query="$*" --prompt=' 󱘖 SSH Work  ' --height=~50% --layout=reverse --border --exit-0)
  ssh $HOST
}

## AI

function fai () {
  export OPENAI_API_KEY=$(op item get "OpenAI API Key" --fields label="api key")
}

## Env

function fetch_secrets () {
  cmd='op read "op://private/ZSH ENV/env"'
  cfg=$(eval $cmd)
  for i in $cfg; do
    echo export $i;
  done > $HOME/.zshenv_private
}
if [[ ! -f $HOME/.zshenv_private ]]; then
  fetch_secrets
  source $HOME/.zshenv_private
fi


## Vim quit

alias :q="exit"
alias :Q="exit"

## ls aliases

if hash eza 2>/dev/null; then
  alias ls='eza --group-directories-first --icons'
elif hash exa 2>/dev/null; then
  alias ls='exa --group-directories-first --icons'
else
  alias ls='ls --group-directories-first --color=auto'
fi

alias ll='ls -l --git'        # Long format, git status
alias l='ll -a'               # Long format, all files
alias lr='ll -T'              # Long format, recursive as a tree
alias lx='ll -sextension'     # Long format, sort by extension
alias lk='ll -ssize'          # Long format, largest file size last
alias lt='ll -smodified'      # Long format, newest modification time last
alias lc='ll -schanged'       # Long format, newest status change (ctime) last

alias tree="exa --icons --tree --level 3"

# go-task

if ! hash task 2>/dev/null; then
  alias task="go-task"
fi

# Network aliases
alias wifi="nmtui"

# yabai

if hash yabai 2>/dev/null; then
  alias yabai-windows="yabai -m query --windows | jq 'map({ \"id\": .id, \"app\": .app, \"title\": .title})'"
  alias yabai-windows-full="yabai -m query --windows"
fi

# readers

function hn() {
  clx -c $(tput cols)
}
alias lob="neonmodem"

# functions

function zr () { zellij run --name "$*" --close-on-exit -- zsh -ic "$*";}
function zrd () { zellij run --name "$*" --direction "Down" --close-on-exit -- zsh -ic "$*";}
function zrf () { zellij run --name "$*" --floating --close-on-exit -- zsh -ic "$*";}
function ze () { zellij edit "$*";}
# function zed () { zellij edit --direction "Down" "$*";}
function zef () { zellij edit --floating "$*";}
function zl () {
  ZJ_LAYOUT="$(ls -1 $HOME/.config/zellij/layouts | sed 's/.kdl//g' | grep -vE '(^_)|(\.swap$)' | sk)"
  zellij action new-tab --layout $ZJ_LAYOUT
}

function fill () {
  sub=${1:=0}
  if hash term-height; then
    TERM_HEIGHT=$(( $(term-height) - 3 - $sub))
  else
    TERM_HEIGHT="$(( $(tput cols) - 2 - $sub ))"
  fi
  for i in $(seq 1 $TERM_HEIGHT); do echo; done
}

function lines() {
  for i in $(seq 0 150); do echo; done
}

# Disable alacritty dock bouncing https://github.com/alacritty/alacritty/issues/2950
printf "\e[?1042l"


source $HOME/.zshrc_brew
